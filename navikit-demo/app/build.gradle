plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    namespace 'com.yandex.navikitdemo'
    compileSdk 33
    buildToolsVersion "33.0.0"

    defaultConfig {
        applicationId "com.yandex.navikitdemo"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "MAPKIT_API_KEY", "\"${mapkitApiKey}\""

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    if (project.properties["with_sign"]) {
        signingConfigs {
            release {
                storeFile file(project.properties['keystorePath'])
                storePassword project.properties['keystorePass']
                keyAlias project.properties['keyAlias']
                keyPassword project.properties['keyPass']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    implementation project(":com.yandex.navikitdemo.di")
    implementation project(":com.yandex.navikitdemo.domain")
    implementation project(":com.yandex.navikitdemo.ui")

    implementation "com.yandex.android:maps.mobile:4.10.0-navikit"
    implementation "com.yandex.mapkit.styling:automotivenavigation:4.10.0"
    implementation "com.yandex.mapkit.styling:roadevents:4.10.0"

    implementation "androidx.appcompat:appcompat:${rootProject.ext.appcompatVersion}"
    implementation "androidx.constraintlayout:constraintlayout:${rootProject.ext.constraintlayoutVersion}"
    implementation "androidx.core:core-ktx:${rootProject.ext.coreKtxVersion}"
    implementation "androidx.fragment:fragment-ktx:${rootProject.ext.fragmentKtxVersion}"
    implementation "androidx.navigation:navigation-fragment:${rootProject.ext.navigationVersion}"
    implementation "androidx.navigation:navigation-ui:${rootProject.ext.navigationVersion}"
    implementation "com.google.android.gms:play-services-location:${rootProject.ext.gmsVersion}"
    implementation "com.google.android.material:material:${rootProject.ext.materialVersion}"
    implementation "com.google.dagger:hilt-android:${rootProject.ext.hiltVersion}"
    kapt "com.google.dagger:hilt-compiler:${rootProject.ext.hiltVersion}"
}

kapt {
    correctErrorTypes true
}
